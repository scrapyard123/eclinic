// SPDX-License-Identifier: MIT

import {
    java.lang.Iterable
    java.lang.Long
    java.lang.Object

    java.time.OffsetDateTime

    org.springframework.beans.factory.annotation.Autowired

    org.springframework.web.bind.annotation.GetMapping
    org.springframework.web.bind.annotation.PathVariable
    org.springframework.web.bind.annotation.PostMapping
    org.springframework.web.bind.annotation.RequestBody
    org.springframework.web.bind.annotation.RequestMapping
    org.springframework.web.bind.annotation.RestController

    eclinic.model.Entry
    eclinic.data.EntryRepository
}

class eclinic.webapp.controller.EntryController [
    RestController
    RequestMapping[value { "/entries" }]
]

constructor():void { }

repository:EntryRepository [Autowired]

create(entry:Entry [RequestBody]):Entry [
    PostMapping[value { "/" }]
] {
    entry->setDateTime(OffsetDateTime->now())
    return [Entry]repository->save([Object]entry)
}

findAll():Iterable [
    GetMapping[value { "/" }]
] {
    return repository->findAll()
}

findById(
    id:Long [
        PathVariable[name { "id" }]
    ]
):Entry [
    GetMapping[value { "/{id}" }]
] {
    return [Entry]repository->findById([Object]id)->get()
}
